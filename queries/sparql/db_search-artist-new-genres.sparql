PREFIX mo: <http://purl.org/ontology/mo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

select ?db_artist_name ?artist ?db_genre_name ?db_artist ?db_year ?artist_year
where {
    service <http://dbpedia.org/sparql> {
        ?db_artist foaf:name ?db_artist_name_en .
        ?db_artist rdf:type dbo:Agent .
        ?db_artist dbo:activeYearsStartYear ?db_year .
        ?db_artist dbo:genre ?db_genre .
        ?db_genre rdfs:label ?db_genre_name_en .
        bind(str(?db_artist_name_en) as ?db_artist_name) .
        filter(langMatches(lang(?db_genre_name_en), "en")) .
        bind(replace(lcase(str(?db_genre_name_en)),"[\\(.*\\)]","") AS ?db_genre_name) .
    }
    filter exists {
        ?genre a mo:Genre .
   		?genre rdfs:label ?db_genre_name
    }
    ?artist a ?type .
    filter(?type in (mo:MusicArtist, mo:MusicGroup)) . 
    ?artist rdfs:label ?db_artist_name .
    ?artist mo:activity_start ?artist_year
    filter(str(?db_year) = str(?artist_year))
    filter not exists {
        ?artist dbo:genre ?artist_genre .
        ?artist_genre a mo:Genre .
        ?artist_genre rdfs:label ?db_genre_name
    }
}